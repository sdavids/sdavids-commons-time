/*
 * Copyright (c) 2017-2018, Sebastian Davids
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static java.io.File.createTempFile
import static java.time.OffsetDateTime.now
import static java.time.ZoneOffset.UTC
import static java.time.format.DateTimeFormatter.ISO_DATE_TIME

buildscript {
  ext {
    forbiddenapisVersion = '2.3'
  }

  repositories {
    jcenter()
  }

  // https://bintray.com/bintray/jcenter
  dependencies {
    classpath("de.thetaphi:forbiddenapis:${forbiddenapisVersion}")
  }
}

// https://plugins.gradle.org/
plugins {
  id 'org.dm.bundle' version '0.10.0'
  id 'de.fuerstenau.buildconfig' version '1.1.8'
  id 'net.nemerosa.versioning' version '2.6.1'
  id 'com.diffplug.gradle.spotless' version '3.14.0'
  id 'net.ltgt.errorprone' version '0.0.11'
  id 'com.jfrog.bintray' version '1.7.3'
}

ext {
  rootPackageName = 'io.sdavids.commons.time'

  // https://bintray.com/bintray/jcenter
  errorproneVersion = '2.1.1'
  guavaVersion = '23.0'
  jsr305Version = '3.0.2'
  joolVersion = '0.9.12'
  junitVersion = '4.12'
  assertjVersion = '3.8.0'
  commonsTestVersion = '1.1.1'
  commonsTestJUnit4 = '1.1.1'

  now = now(UTC)
  nowIsoDateTime = now.format(ISO_DATE_TIME)
  buildJdk = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
  vendor = 'Sebastian Davids'

  javadocLinks = [
      'http://docs.oracle.com/javase/8/docs/api/'
  ] as String[]
}

group 'io.sdavids.commons'
version = '1.1.0'

repositories {
  mavenLocal()
  jcenter()
}

apply plugin: 'java-library'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'de.thetaphi.forbiddenapis'
apply plugin: 'maven'

dependencies {
  errorprone("com.google.errorprone:error_prone_core:${errorproneVersion}")

  compileOnly("com.google.code.findbugs:jsr305:${jsr305Version}")

  testImplementation("io.sdavids.commons.test:sdavids-commons-test:${commonsTestVersion}")
  testImplementation("io.sdavids.commons.test:sdavids-commons-test-junit4:${commonsTestJUnit4}")
  testImplementation("com.google.guava:guava:${guavaVersion}") {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
  }
  testImplementation("org.jooq:jool:${joolVersion}")
  testImplementation("junit:junit:${junitVersion}")
  testImplementation("org.assertj:assertj-core:${assertjVersion}")
}

sourceCompatibility = '1.8'

compileJava.options.debugOptions.debugLevel = 'source,lines,vars'

[compileJava, compileTestJava]*.options.collect { options ->
  options.encoding = 'UTF-8'
}

[compileJava, compileTestJava]*.options.collect { options ->
  options.debug = true
  options.deprecation = true
  options.warnings = true
}

// http://docs.oracle.com/javase/8/docs/technotes/tools/windows/javac.html#BHCJCABJ
compileJava.options.collect { options ->
  options.compilerArgs << '-parameters'
  options.compilerArgs << '-Xlint:all,-processing,-path'
  options.compilerArgs << '-Werror'
}

// http://docs.oracle.com/javase/8/docs/technotes/tools/windows/javac.html#BHCJCABJ
compileTestJava.options.collect { options ->
  options.compilerArgs << '-parameters'
  options.compilerArgs << '-Xlint:all,-processing,-path'
}

buildConfig {
  clsName = 'VersionInfo'
  packageName = rootPackageName
  version = "${project.version}-${versioning.info.build}"
  buildConfigField 'String', 'REVISION', versioning.info.commit
  buildConfigField 'String', 'BUILD_JDK', buildJdk
  buildConfigField 'java.time.OffsetDateTime', 'BUILD_TIME', "java.time.OffsetDateTime.parse(\"${nowIsoDateTime}\")"
}

jar {
  manifest {
    attributes(
        'Created-By': "Gradle ${gradle.gradleVersion}",
        'Built-By': project.author,
        'Build-Jdk': buildJdk,
        'Build-Time': nowIsoDateTime,
        'Build-Revision': versioning.info.commit,
        'Build-Host': "${InetAddress.localHost.hostName}",
        'Specification-Title': project.name,
        'Specification-Version': project.version,
        'Specification-Vendor': project.vendor,
        'Implementation-Title': project.name,
        'Implementation-Version': "${project.version}-${versioning.info.build}",
        'Implementation-Vendor': project.vendor,
        ("Version-${project.name}".toString().capitalize()): project.version,
        ("Time-${project.name}".toString().capitalize()): nowIsoDateTime,
        ("Revision-${project.name}".toString().capitalize()): versioning.info.commit,
        'Automatic-Module-Name': rootPackageName
    )
  }
}

bundle {
  instructions << [
      'Bundle-SymbolicName'  : rootPackageName,
      'Bundle-Name'          : project.name,
      'Bundle-License'       : 'http://www.apache.org/licenses/LICENSE-2.0.txt',
      'Bundle-Vendor'        : project.vendor,
      'Bundle-ContactAddress': 'sdavids@gmx.de',
      'Import-Package'       : 'javax.annotation;resolution:=optional,*',
      'Export-Package'       : "${group}.*;-split-package:=merge-first",
      'Provide-Capability'   : 'osgi.serviceloader;osgi.serviceloader=ClockSupplier',
      'Require-Capability'   : 'osgi.extender;filter:="(osgi.extender=osgi.serviceloader.registrar)",osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.8))"'
  ]
}

forbiddenApis {
  // https://github.com/policeman-tools/forbidden-apis/wiki/BundledSignatures
  bundledSignatures = [
      'jdk-unsafe',
      'jdk-deprecated',
      'jdk-non-portable',
      'jdk-system-out',
      'jdk-reflection'
  ]
}

spotless {
  format 'misc', {
    target '.gitattributes', '.gitignore',
        'README.adoc',
        'release.sh',
        'CODING_STYLE.txt',
        'AUTHORS', 'CONTRIBUTORS', 'LICENSE', 'NOTICE'
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'properties', {
    target 'gradle.properties'
    encoding 'ISO-8859-1'
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'yaml', {
    target '.travis.yml'
    indentWithSpaces 2
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'xml', {
    target 'gradle/conf/**/*.xml'
    indentWithSpaces 2
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    googleJavaFormat '1.6'
    licenseHeaderFile "${project.projectDir}/gradle/conf/spotless-license-java.txt"
  }
  groovyGradle {
    target 'build.gradle', 'settings.gradle'
  }
}

test {
  systemProperty 'user.timezone', System.properties['user.timezone']

  // injectable singleton needs a new VM for each test case
  forkEvery 1

  testLogging {
    exceptionFormat = 'full'
  }
}

wrapper {
  gradleVersion = '4.10'
}

if (project.hasProperty('release')) {

  apply plugin: 'signing'
  apply plugin: 'maven-publish'

  task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    dirMode = 0775
    fileMode = 0664
    manifest {
      attributes(
          'Build-Revision': versioning.info.commit,
          'Specification-Title': project.name,
          'Specification-Version': project.version,
          'Specification-Vendor': project.vendor,
          'Implementation-Title': project.name,
          'Implementation-Version': "${project.version}-${versioning.info.build}",
          'Implementation-Vendor': project.vendor
      )
    }
    from javadoc.destinationDir
  }

  task sourcesJar(type: Jar, dependsOn: 'classes') {
    classifier = 'sources'
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    dirMode = 0775
    fileMode = 0664
    manifest {
      attributes(
          'Build-Revision': versioning.info.commit,
          'Specification-Title': project.name,
          'Specification-Version': project.version,
          'Specification-Vendor': project.vendor,
          'Implementation-Title': project.name,
          'Implementation-Version': "${project.version}-${versioning.info.build}",
          'Implementation-Vendor': project.vendor
      )
    }
    from sourceSets.main.allSource exclude 'rebel.xml'
  }

  javadoc {
    options.encoding = 'UTF-8'
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.header = "${project.name} ${project.version}"
    options.docTitle = "${project.name} API"
    options.links = javadocLinks
    options.addStringOption '-quiet'
  }

  artifacts {
    archives javadocJar, sourcesJar
  }

  signing {
    sign configurations.archives
  }

  final pomConfig = {
    name "${project.name}"
    licenses {
      license {
        name 'Apache License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
    developers {
      developer {
        id 'sdavids'
        name 'Sebastian Davids'
        email 'sdavids@gmx.de'
        timezone 'Europe/Berlin'
      }
    }
    scm {
      url 'https://github.com/sdavids/sdavids-commons-time'
      connection 'scm:git:https://github.com/sdavids/sdavids-commons-time.git'
    }
    issueManagement {
      system 'GitHub'
      url 'https://github.com/sdavids/sdavids-commons-time/issues'
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourcesJar {
          classifier 'sources'
        }

        artifact javadocJar {
          classifier 'javadoc'
        }

        pom.withXml {
          asNode().children().last() + pomConfig

          // workaround
          // "Signing the generated POM file generated by this plugin is currently not supported."
          // https://docs.gradle.org/current/userguide/publishing_maven.html
          final tmp = createTempFile("generated-pom", ".xml")
          tmp.deleteOnExit()

          writeTo(tmp)

          final pomAscFile = signing.sign(tmp).signatureFiles[0]

          artifact(pomAscFile) {
            classifier = null
            extension = 'pom.asc'
          }

          tmp.delete()
          //
        }

        // workaround
        // asc files not uploaded
        project.tasks.signArchives.signatureFiles.each {
          artifact(it) {
            def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
            classifier = matcher.find() ? matcher.group(1) : null
            extension = 'jar.asc'
          }
        }
        //
      }
    }
  }

  bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
      repo = 'sdavids'
      name = 'sdavids-commons-time'
      licenses = ['Apache-2.0']
      websiteUrl = 'https://github.com/sdavids/sdavids-commons-time'
      issueTrackerUrl = 'https://github.com/sdavids/sdavids-commons-time/issues'
      vcsUrl = 'https://github.com/sdavids/sdavids-commons-time.git'
      githubRepo = 'sdavids/sdavids-commons-time'
      publicDownloadNumbers = true
      version {
        name = "${project.version}"
        vcsTag = "v${project.version}"
      }
    }
  }
}
